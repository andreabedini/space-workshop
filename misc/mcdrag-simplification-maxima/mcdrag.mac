/*****************************************************************************\
*
*  *-------------------*
*  | MC Drag Equations |
*  *-------------------*
*
*  This is a Maxima file: http://maxima.sourceforge.net/
*
*  This file contains a symbolic manipulation of relevant equations from:
*  
*   McCoy, Robert L (1981) "MC DRAG" - A Computer Program for Estimating the
*     Drag Coefficients of Projectiles. Technical Report ARBRL-TR-02293.
*     US Army Armament Research and Development Command, Ballistic Research
*     Laboratory, Aberdeen Proving Ground, Maryland.
*
*   URL: https://apps.dtic.mil/docs/citations/ADA098110
*
*  The purpose of reproducing the equations here are as follows:
*
*   1. To export the equations as LaTeX to check them. It is much easier to
*      check long equations visually in rendered LaTeX output than in raw
*      Haskell.
*
*   2. To extract smaller terms from the equations that can be more
*      comfortably authored in Haskell. This could be done manually, but is
*      error-prone, and thus better suited to a CAS system. The potential
*      for errors is exacerbated by the length and complexity of the
*      equations.
*
*  Contact: Jonathan Merritt <j.s.merritt@gmail.com>
*
\****************************************************************************/

simp : false;


/*************
 * HEAD DRAG *
 *************/

/* Head drag constants. Eq. 2 */
texput(R_T, "R_T");
C_1 : 0.7156 - 0.5313*('R_T/'R) + 0.5950*('R_T/'R)^2;
C_2 : 0.0796 + 0.0779*('R_T/'R);
C_3 : 1.587 + 0.049*('R_T/'R);
C_4 : 0.1122 + 0.1658*('R_T/'R);

/* Tau incorporating meplat. Eq. 3 */
texput(tau, "\\tau");
texput(d_M, "d_M");
texput(L_N, "L_N");
tau : (1 - 'd_M)/'L_N;

/* Supersonic head drag, incorporating meplat. Eq. 4 */
texput(C_DH, "C_{DH}");
texput(M, "M_{\\infty}");
texput(d_M, "d_M");
texput(C_PS, "C_{PS}");
C_DH : ('C_1 - 'C_2*'tau^2)/('M^2 - 1)*('tau*sqrt('M^2 - 1))^('C_3+'C_4*'tau)
       + (pi/4)*'K*'d_M^2*'C_PS;

/* Transonic head drag, incorporating meplat. Eq. 8 */
texput(C_DHT, "C_{DHT}");
texput(gamma, "\\gamma");
texput(M_C, "M_C");
C_DHT : 0.368*'tau^(9/5) + (1.6*'tau*('M^2 - 1))/(('gamma + 1)*'M^2);
M_C : sqrt(1 + 0.552*'tau^(4/5));


/*****************
 * BOATTAIL DRAG *
 *****************/

texput(A_1, "A_1");
texput(L_CYL, "L_{CYL}");
texput(beta, "\\beta");
texput(L_BT, "L_{BT}");

/* Constants in support of Eq. 9 */
k : 0.85 / (sqrt('M^2 - 1));
A_1 : (1 - 3*('R_T/'R)/(5*'M))
      *(5*'tau/(6*sqrt('M^2 - 1))*('tau/2)^2 - 0.7435/('M^2)*('tau*'M)^1.6);
A : 'A_1*e^(-sqrt(2/('gamma*'M^2))*'L_CYL) + 2*tan('beta)/(sqrt('M^2-1))
    - (('gamma + 1)*'M^4 - 4*('M^2 - 1))*(tan('beta))^2/(2*('M^2 - 1)^2);

/* Supersonic boattail drag similarity parameter. Eq. 9 */
C_DBT : 4*'A*tan('beta)/'k*((1-e^(-'k*'L_BT))
        + 2*tan('beta)*(e^(-'k*'L_BT)*('L_BT + (1/'k)) - (1/'k)));

/* Transonic boattail drag similarity parameter. Eq. 10 */
C_DBTT : 4*(tan('beta))^2*(1 + (1/2)*tan('beta))*(1 - e^(-2*'L_BT)
         + 2*tan('beta)*(e^(-2*'L_BT)*('L_BT + (1/2)) - (1/2)));


/**********************************
 * SKIN FRICTION DRAG COEFFICIENT *
 **********************************/

texput(C_DSF, "C_{DSF}");
texput(C_F, "C_F");
texput(S_W, "S_W");
texput(Re_L, "Re_L");
texput(U, "U_{\\infty}");
texput(L_T, "L_T");
texput(nu, "\\nu");
texput(S_WCYL, "S_{W(CYL)}");
texput(S_WNOSE, "S_{W(NOSE)}");
texput(C_FT, "C_{FT}");
texput(C_FL, "C_{FL}");

/* Reynold's Number */
Re_L : 'U * 'L_T / 'nu;

/* Laminary boundary layer skin friction coefficient. Eq. 12 */
C_FL : 1.328/(sqrt('Re_L))*(1 + 0.12*'M^2)^(-0.12);

/* Turbulent boundary layer skin friction coefficient. Eq. 13 */
C_FT : 0.455/(log10('Re_L))^2.58*(1 + 0.21*'M^2)^(-0.32);

/* Wetted area of the projectile nose. Eq. 14 */
S_WNOSE : pi/2*'L_N*(1+1/(8*'L_N^2))*(1+(1/3 + 1/(50*'L_N^2))*('R_T/'R));

/* Wetted area of the projectile afterbody. Eq. 15 */
S_WCYL : pi * ('L_T - 'L_N);

/* Overall skin firction drag. Eq. 11 */
C_DSF : 4/pi*'C_F*'S_W;


/****************************************************
 * BASE DRAG COEFFICIENT (BLUNT BASE OF PROJECTILE) *
 ****************************************************/

texput(P_BI, "P_{B\\infty}");
texput(d_B, "d_B");
texput(C_DB, "C_{DB}");

/* Pressure ratio. Eq. 18 */
P_BI : (1 + 0.09*'M^2*(1-e^(-'L_CYL)))*(1 + (1/4)*'M^2*(1-'d_B));

/* Base drag coefficient. Eq. 19 */
C_DB : 2*'d_B^2/('gamma*'M^2)*(1 - 'P_BI);


/***********************************
 * SIMPLIFICATION AND SUBSTITUTION *
 ***********************************/

/* Sub-expressions. */

texput(hsp, "\\frac{R_T}{R}");
texput(c01, "c_{01}");
texput(c02, "c_{02}");
texput(c03, "c_{03}");
texput(c04, "c_{04}");
texput(c05, "c_{05}");
texput(c06, "c_{06}");
texput(c07, "c_{07}");
texput(c08, "c_{08}");
texput(c09, "c_{09}");
texput(c10, "c_{10}");
texput(c11, "c_{11}");
texput(c12, "c_{12}");
texput(c13, "c_{13}");
texput(c14, "c_{14}");
texput(c15, "c_{15}");
texput(c16, "c_{16}");
texput(c17, "c_{17}");
texput(c18, "c_{18}");
texput(c19, "c_{19}");
texput(c20, "c_{20}");
texput(c21, "c_{21}");
texput(c22, "c_{22}");
texput(c23, "c_{23}");
texput(c24, "c_{24}");
texput(c25, "c_{25}");
texput(c26, "c_{26}");
texput(c27, "c_{27}");
texput(c28, "c_{28}");
texput(c29, "c_{29}");
texput(c30, "c_{30}");
texput(c31, "c_{31}");

hsp : 'R_T/'R;
c01 : 'M^2 - 1;
c02 : sqrt('c01);
c03 : 'C_1 - 'C_2*'tau^2;
c04 : 'C_3 + 'C_4*'tau;
c05 : pi/4*'K*'d_M^2*'C_PS;
c06 : 0.368*'tau^(9/5);
c07 : 'gamma + 1;
c08 : 1.6*'tau*'c01/('c07*'M^2);
c09 : 1 - 3*('R_T/'R)/(5*'M);
c10 : 5*'tau/(6*'c02)*('tau/2)^2;
c11 : 0.7435/('M^2)*('tau*'M)^1.6;
c12 : sqrt(2/('gamma*'M^2));
c13 : tan('beta);
c14 : -('c07*'M^4 - 4*'c01);
c15 : e^(-'c12*'L_CYL);
c16 : 'c14 * 'c13^2 / (2 * 'c01^2);
c17 : e^(-'k*'L_BT);
c18 : e^(-2*'L_BT);
c19 : 'c17*('L_BT + 1/'k) - 1/'k;
c20 : 'c18*('L_BT + 1/2) - 1/2;
c21 : 4*'A*'c13/'k;
c22 : 1 + (1/2) * 'c13;
c23 : 1.328 / sqrt('Re_L);
c24 : (1 + 0.12*'M^2)^(-0.12);
c25 : 0.455 / (log10('Re_L))^2.58;
c26 : (1 + 0.21*'M^2)^(-0.32);
c27 : 1 + 1 / (8*'L_N^2);
c28 : (1/3) + 1 / (50*'L_N^2);
c29 : 1 + 1/4*'M^2*(1 - 'd_B);
c30 : 1 - e^(-'L_CYL);
c31 : 2*'d_B^2/('gamma*'M^2);

substitutions :
    [ hsp = 'hsp
    , c01 = 'c01
    , c02 = 'c02
    , c03 = 'c03
    , c04 = 'c04
    , c05 = 'c05
    , c06 = 'c06
    , c07 = 'c07
    , c08 = 'c08
    , c09 = 'c09
    , c10 = 'c10
    , c11 = 'c11
    , c12 = 'c12
    , c13 = 'c13
    , c14 = 'c14
    , c15 = 'c15
    , c16 = 'c16
    , c17 = 'c17
    , c18 = 'c18
    , c19 = 'c19
    , c20 = 'c20
    , c21 = 'c21
    , c22 = 'c22
    , c23 = 'c23
    , c24 = 'c24
    , c25 = 'c25
    , c26 = 'c26
    , c27 = 'c27
    , c28 = 'c28
    , c29 = 'c29
    , c30 = 'c30
    , c31 = 'c31
    ];

/* Supersonic head drag coefficient. */

C_DH_S : subst(substitutions, C_DH);

/* Transonic head drag coefficient. */

C_DHT_S : subst(substitutions, C_DHT);

/* Boattail pressure recovery factor. */

k_S : subst(substitutions, k);

/* Factors for Prandtl-Meyer expansion. */

A_1_S : subst(substitutions, A_1);
A_S : subst(substitutions, A);

/* Similarity parameters for boattail drag. */
C_DBT_S : subst(substitutions, C_DBT);
C_DBTT_S : subst(substitutions, C_DBTT);

/* Skin friction drag. */
C_FL_S : subst(substitutions, C_FL);
C_FT_S : subst(substitutions, C_FT);

/* Wetted areas. */
S_WNOSE_S : subst(substitutions, S_WNOSE);

/* Pressure ratio. */
P_BI_S : subst(substitutions, P_BI);

/* Base drag coefficient. */
C_DB_S : subst(substitutions, C_DB);
